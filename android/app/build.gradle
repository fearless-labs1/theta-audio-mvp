plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
}

def envKeystorePath = System.getenv('ANDROID_KEYSTORE_PATH')
def envKeystorePassword = System.getenv('ANDROID_KEYSTORE_PASSWORD')
def envKeyAlias = System.getenv('ANDROID_KEY_ALIAS')
def envKeyPassword = System.getenv('ANDROID_KEY_PASSWORD')

def hasEnvSigning = envKeystorePath && envKeystorePassword && envKeyAlias && envKeyPassword
def hasFileSigning = keystoreProperties.containsKey('storeFile')

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.fearlesslabs.theta_audio_mvp"
    compileSdk 36
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.fearlesslabs.theta_audio_mvp"
        minSdk 24
        targetSdk 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            if (hasEnvSigning) {
                storeFile file(envKeystorePath)
                storePassword envKeystorePassword
                keyAlias envKeyAlias
                keyPassword envKeyPassword
            } else if (hasFileSigning) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                initWith signingConfigs.debug
            }
        }
    }

    buildTypes {
        release {
            signingConfig hasEnvSigning || hasFileSigning ? signingConfigs.release : signingConfigs.debug
        }
    }
}

flutter {
    source '../..'
}

dependencies {}